# 链接脚本

------
## 1. 目的
1. 规定如何把**输入文件的section放入输出文件内**
2. 控制**输出文件内各部分在程序地址空间内的布局**
3. 其它

------
## 2. 基本概念
- 由一系列命令组成, 每条命令由**一个关键字**或一条**对符号的赋值语句**组成
- 命令由分号**;**分隔开.
- 文件名或格式名内如果包含**分号或其他分隔符**, 则要用**引号**将名字全称引用起来
- 无法处理含引号的文件名
- 使用/* */注释

### 2.1 内置链接脚本
- 链接器有个默认脚本，如下：
> 如果没有特殊说明，以下指的都是**静态链接器**
```
asb@docker:~$ ld --verbose
GNU ld (GNU Binutils for Ubuntu) 2.24
  Supported emulations:
   elf_x86_64
   elf32_x86_64
   elf_i386
   i386linux
   elf_l1om
   elf_k1om
   i386pep
   i386pe
using internal linker script:
==================================================
/* Script for -z combreloc: combine and sort reloc sections */
OUTPUT_FORMAT("elf64-x86-64", "elf64-x86-64",
              "elf64-x86-64")
OUTPUT_ARCH(i386:x86-64)
ENTRY(_start)
SEARCH_DIR("/usr/x86_64-linux-gnu/lib64"); SEARCH_DIR("=/usr/local/lib/x86_64-linux-gnu"); SEARCH_DIR("=/usr/local/lib64"); SEARCH_DIR("=/lib/x86_64-linux-gnu"); SEARCH_DIR("=/lib64"); SEARCH_DIR("=/usr/lib/x86_64-linux-gnu"); SEARCH_DIR("=/usr/lib64"); SEARCH_DIR("=/usr/local/lib"); SEARCH_DIR("=/lib"); SEARCH_DIR("=/usr/lib");
SECTIONS
{
 ......
}
==================================================
```
### 2.2 概念
- **输入文件**：目标文件或者链接脚本文件     
- **输出文件**：目标文件或者可执行文件
- 目标文件具有固定的格式, 在UNIX或GNU/Linux平台下, 一般为**ELF格式**
- 目标文件的section至少包含两个信息: **名字**和**大小**. 大部分section还包含与**它相关联的一块数据**, 称为section contents(section内容)
- 一个section可被标记为**loadable(可加载的)**或**allocatable(可分配的)**
> 在输出文件运行时, loadable section的内容将被**载入进程地址空间中**；在进程地址空间中**空出大小和allocatable section指定大小的部分**，某些情况下，这块内存被置位0    

- 如果一个section不是以上两种，则其通常包含了调试信息. 可用**objdump -h**命令查看相关信息
> **VMA虚拟地址空间**和**LMA加载地址空间**的区别？    

- 符号(symbol): 每个目标文件都有符号表(SYMBOL TABLE), 包含**已定义的符号**(对应全局变量和static变量和定义的函数的名字)和**未定义符号**(未定义的函数的名字和引用但没定义的符号)信息
- 符号值: 每个符号**对应一个地址**，使用**nm**查看符号值


------
## 3. 示例
- 以下脚本将输出文件的text section定位在0×10000, data section定位在0×8000000
```
SECTIONS
{
    . = 0×10000;            // 把定位器符号的值置为0×10000 (若不指定, 则该符号的初始值为0)
    .text : { *(.text) }    // 将所有输入文件的.text section合并成一个.text section, 该section的地址由定位器符号的值指定, 即0×10000
    . = 0×8000000;          // 把定位器符号的值置为0×8000000
    .data : { *(.data) }    // 将所有输入文件的.data section合并成一个.data section, 该section的地址被置为0×8000000
    .bss : { *(.bss) }      // 将所有输入文件的.bss section合并成一个.bss section，该section的地址被置为0×8000000+.data section的大小
}

```
- 连接器每读完一个section描述后, 将定位器符号的值**增加**该section的大小
> 不考虑对齐约束的情况下


------
## 4. 简单脚本命令
- **ENTRY(SYMBOL)**：将符号**SYMBOL**的值设置为入口地址
> 入口地址(entry point)是指进程执行的**第一条用户空间的指令在进程地址空间的地址**      
> ld有多种方法设置**进程入口地址**, 按以下顺序:(**编号越前, 优先级越高**)      
1. ld命令行的-e选项      
2. 连接脚本的ENTRY(SYMBOL)命令      
3. 如果定义了start符号, 使用start符号值      
4. 如果存在.text section, 使用.text section的第一字节的位置值      
5. 使用值0      

- **INCLUDE filename**：包含名为filename的链接脚本，类似于c中**#include**指令    
> 脚本搜索路径**由-L选项**指定；INCLUDE指令可以嵌套使用, 最大深度为10，即文件1内INCLUDE文件2…文件10内INCLUDE文件11. 则文件11内不能再出现INCLUDE指令了

- **INPUT(files)**：将files做为链接过程的输入文件
> ld首先在当前目录下寻找该文件, 如果没找到, 则在由-L指定的搜索路径下搜索；file可以为-lfile形式，就象命令行的-l选项一样    
> 如果该命令出现在暗含的脚本内, 则该命令内的file在链接过程中的顺序由**该暗含的脚本在命令行内的顺序决定**

- **GROUP(files)**：指定需要**重复搜索符号定义**的多个输入文件
> file必须是库文件, 且file文件作为一组被ld重复扫描，直到**不再有新的未定义的引用**出现

- **OUTPUT(FILENAME)**：定义输出文件的名字
> 同ld的-o选项, 不过-o的优先级更高；它可以用来**定义默认的输出文件名**， 如a.out

- **SEARCH_DIR(PATH)**：定义搜索路径
> 同ld的-L选项, 但是-L指定的路径**会优先被搜索**

- **STARTUP(filename)**：指定filename为第一个输入文件
> 链接过程中, 输入文件是**有顺序的**，此命令设置文件filename为第一个输入文件

- **OUTPUT_FORMAT(BFDNAME)**：设置输出文件使用的BFD格式
> 同ld选项-o format BFDNAME, ld选项优先级更高

- **OUTPUT_FORMAT(DEFAULT,BIG,LITTLE)**：定义三种输出文件的格式(大小端)
> 若有命令行**选项-EB**，使用**第2个BFD格式**；若有命令行**选项-EL**，则使用**第3个BFD格式**；否则**默认选第一个BFD格式**

- **TARGET(BFDNAME)**：设置输入文件的BFD格式
> 同ld选项-b BFDNAME; 若**使用了TARGET命令**, 但**未使用OUTPUT_FORMAT命令**, 则用最后一个TARGET命令设置的BFD格式将**被作为输出文件的BFD格式**      
> 这边可能有点问题？

- **ASSERT(EXP, MESSAGE)**：如果EXP不为真，终止链接过程

- **EXTERN(SYMBOL SYMBOL …)**：在输出文件中增加未定义的符号，链接器选项-u

- **FORCE\_COMMON\_ALLOCATION**：为common symbol(通用符号)分配空间
> 同-r链接选项

- **NOCROSSREFS(SECTION SECTION …)**：检查列出的输出section，如果发现**它们之间有相互引用，则报错**
> 对于某些系统，特别是内存较紧张的嵌入式系统，**某些section是不能同时存在内存中的，所以他们之间不能相互引用**

- **OUTPUT_ARCH(BFDARCH)**：设置输出文件的machine architecture(体系结构)，BFDARCH为BFD库使用的名字之一
> 用命令objdump -f查看

**可通过 man -S 1 ld查看ld的联机帮助, 里面也包括了对这些命令的介绍**

------
## 5. 符号赋值
- 在目标文件内定义的符号可以在链接脚本内被赋值，每个符号都对应了一个地址, 赋值就是更改这个符号对应的地址
- 例子：
> 注：**符号的赋值只对全局变量起作用**       

```
/* a.c */
#include <stdio.h>
int a = 100;

int main()
{
    printf( "&a=%p\n", &a );
    return 0;
}

/* a.lds */
a = 3;

$ gcc -Wall -o a-without-lds.exe a.c
&a = 0×601020

$ gcc -Wall -o a-with-lds.exe a.c a.lds
&a = 0×3
```

- 简单的赋值语句，可以使用c的语法
```
SYMBOL = EXPRESSION;
SYMBOL += EXPRESSION;
SYMBOL -= EXPRESSION;
SYMBOL *= EXPRESSION;
SYMBOL /= EXPRESSION;
SYMBOL >= EXPRESSION;
SYMBOL &= EXPRESSION;
SYMBOL |= EXPRESSION;
```

- **点**(.)是一个特殊的符号；它是定位器，一个位置指针，**指向程序地址空间内的某位置**
> 赋值语句包含4个元素：**符号名、操作符、表达式、分号**，一个也不能少；被赋值后，**符号所属的section被设值为表达式EXPRESSION所属的SECTION**      

- 赋值语句出现在链接脚本的三个地方：**SECTIONS命令内**，**SECTIONS命令内的section描述内**和**全局位置**   
```
floating_point = 0;                  /* 全局位置 */
SECTIONS
{
    .text :
        {
            *(.text)
            _etext = .;             /* section描述内 */
        }
    _bdata = (. + 3) & ~ 4;         /* SECTIONS命令内 */
    .data : { *(.data) }
}
```

- **PROVIDE**用于**定义在目标文件内被引用，但没有在任何目标文件内定义的符号**
> 目标文件内引用了etext符号，但没有定义时，etext符号对应的地址被定义为**.text section之后的第一个字节的地址**

```
SECTIONS
{
    .text :
    {
        *(.text)
        _etext = .;
        PROVIDE(etext = .);
    }
}
```

------
## 6. SECTIONS命令
- 告诉ld如何把**输入文件的sections映射到输出文件的各个section**；如何将**输入section合为输出section**；如何把**输出section放入程序地址空间(VMA)和进程地址空间(LMA)**
- 格式
```
SECTIONS
{
    SECTIONS-COMMAND
    SECTIONS-COMMAND
    …
}
```
- SECTION-COMMAND有四种: **ENTRY命令**；**符号赋值语句**；**一个输出section的描述(output section description)**；**一个section叠加描述(overlay description)**      
> 如果整个链接脚本内没有SECTIONS命令, 那么ld将所有**同名输入section合成为一个输出section内**, 各输入section的顺序为**它们被连接器发现的顺序**        
> 如果某输入section没有在SECTIONS命令中提到, 那么该section将被**直接拷贝成输出section**       
 
### 6.1 输出section描述
- SECTION-COMMAND格式:
```
SECTION-NAME [ADDRESS] [(TYPE)] : [AT(LMA)]
{
    OUTPUT-SECTION-COMMAND
    OUTPUT-SECTION-COMMAND
    …
} [>REGION] [AT>LMA_REGION] [:PHDR HDR ...] [=FILLEXP]
```
> []内的内容为可选，一般不要      
> SECTION-NAME：section名字；SECTION-NAME**左右的空白、圆括号、冒号是必须的**，换行符和其他空格是可选的     
 
#### 6.1.1 输出section名字
- 必须符合输出文件格式要求，如：a.out格式的文件只允许存在.text、.data和.bss section名；在**只允许存在数字名字**或**名字内包含特殊字符(比如空格、逗号等)**的情况下，需要**用引号将其组合在一起**
 
#### 6.1.2 输出section地址
- [ADDRESS]是一个表达式，其值用于设置VMA
> 如果没有该选项且**有REGION选项**，将**根据REGION设置VMA**；如果也没有REGION选项，链接器将根据**定位符号.**的值设置VMA，将定位符号的值调整到**满足输出section对齐要求后的值(输出section对齐要求为用到的所有输入section的对齐要求中最严格的那个)**

- ADDRESS可以是一个**任意表达式**，比如，ALIGN(0×10)把VMA设置成满足16字节对齐后的修调值
> **设置ADDRESS值，将更改定位符号的值**

```
// VMA设置为定位符号的值
.text . : 
{ 
    *(.text) 
}

// VMA设置为定位符号的修调值，即满足对齐要求后的值
.text : 
{ 
    *(.text)
}
```

#### 6.1.3 输出section描述
- OUTPUT-SECTION-COMMAND可以是：**符号赋值语句**；**输入section描述**；**直接包含的数据值**；**一些特殊的输出section关键字**

**1. 符号赋值语**      
- 见上
 
**2. 输入section描述**      
```
// FILENAME文件名和SECTION名字，可以是一个字符串模式
FILENAME([EXCLUDE_FILE] (FILENAME1 FILENAME2 ...) SECTION1 SECTION2 ...)

// 所有输入文件的.text section
*(.text) 

// 除crtend.o、otherfile.o文件外的所有输入文件的.ctors section
(*(EXCLUDE_FILE (*crtend.o *otherfile.o) .ctors))

// data.o文件的.data section
data.o(.data)

// data.o文件的所有section
data.o

// 所有文件的.text section和.data section，即第一个文件的.text section，.data section，第二个文件的.text section，.data section，...
*(.text .data) 

// 所有文件的.text section和.data section，即第一个文件的.text section，第二个文件的.text section...第一个文件的.data section，第二个文件的.data section
*(.text) *(.data)
```
- 当FILENAME是一个特定的文件名时，链接器会查看它是否在**链接命令行内**出现或在**INPUT命令中**出现
- 当FILENAME是一个字符串模式时，连接器仅仅查看它是否在**链接命令行内**出现
> 如果链接器发现某文件在INPUT命令内出现，那么它会**在-L指定的路径内搜寻该文件**     

- **字符串模式内通配符**，正则表达式里面的内容
> \*表示任意多个字符；?表示任意一个字符；[CHARS]表示任意一个CHARS内的字符，可用-号表示范围，如：a-z；**:表示引用下一个紧跟的字符**         
> 在文件名内，通配符**不匹配文件夹分隔符/，仅包含通配符\*时除外**      
> **链接器依次扫描每个OUTPUT-SECTION-COMMAND命令内的文件名，任何一个文件的任意section只能在SECTIONS命令内出现一次**       
> 使用用-M连接命令选项来产生一个map文件，它包含所有输入section到输出section的组合信息      

```
SECTIONS {
// data.o文件的.data section在第一个OUTPUT-SECTION-COMMAND命令内被使用了，那么在第二个中将不会再被使用，因此即使连接器不报错，输出文件的.data1 section的内容也是空的
.data : { *(.data) }
.data1 : { data.o(.data) }
}

// 所有文件输入.text section组成输出.text section；以大写字母开头的文件的.data section组成输出.DATA section
// 其他文件的.data section组成输出.data section；所有文件的.bss section组成输出.bss section
SECTIONS {
.text : { *(.text) }
.DATA : { [A-Z]*(.data) }
.data : { *(.data) }
.bss : { *(.bss) }
}
```
- 用SORT()关键字对满足字符串模式的所有名字进行递增排序，如**SORT(.text*)**      
 
- **通用符号(common symbol)的输入section**：在许多目标文件格式中，通用符号并没有占用一个section，链接器认为输入文件的**所有通用符号在名为COMMON的section内**       
```
// 将所有输入文件的所有通用符号放入输出.bss section内
.bss { *(.bss) *(COMMON) }
```
> 有些目标文件格式把通用符号分成几类。如，在MIPS elf目标文件格式中，把通用符号分成**standard common symbols**和**small common symbols**，此时链接器认为所有standard common symbols在**COMMON section内**，而small common symbols在.scommon section内，另外在一些以前的连接脚本内可以看见[COMMON]，相当于*(COMMON)，**不建议继续使用这种陈旧的方式**
 
- **输入section和垃圾回收**：在链接命令行内使用了选项–gc-sections后，连接器可能将某些它认为没用的section过滤掉，此时就有必要强制连接器保留一些特定的section，可用**KEEP()关键字达此目的**,如**KEEP(\*(.text))或KEEP(SORT(\*)(.text))**  

```
SECTIONS {

// 将all.o文件的所有section和foo.o文件的所有(一个文件内可以有多个同名section).input1 section依次放入输出outputa section内，该section的VMA是0×10000；
outputa 0×10000 :
{
all.o
foo.o (.input1)
}

// 将foo.o文件的所有.input2 section和foo1.o文件的所有.input1 section依次放入输出outputb section内，该section的VMA是当前定位器符号的修调值(对齐后)
outputb :
{
foo.o (.input2)
foo1.o (.input1)
}

// 将其他文件(非all.o、foo.o、foo1.o)文件的. input1section和.input2 section放入输出outputc section内
outputc :
{
*(.input1)
*(.input2)
}
}
```

**3. 直接包含数据值**      
- 可以**显式地**在输出section内填入你想要填入的信息

```
BYTE(EXPRESSION)  // 1 字节
SHORT(EXPRESSION) // 2 字节
LOGN(EXPRESSION)  // 4 字节
QUAD(EXPRESSION)  // 8 字节
SQUAD(EXPRESSION) // 64位处理器的代码时，8 字节
```

- 输出文件的字节顺序**big endianness**或**little endianness**，可以由输出目标文件的格式决定；如果输出目标文件的格式不能决定字节顺序，那么字节顺序与第一个输入文件的字节顺序相同
> 如：BYTE(1)、LANG(addr)       
> 注意，这些命令**只能放在输出section描述内**           

```
// 错误
SECTIONS { .text : { *(.text) } LONG(1) .data : { *(.data) } }

// 正确
SECTIONS { .text : { *(.text) LONG(1) } .data : { *(.data) } }
```

- 当输出section内存在未描述的存储区域(比如由于对齐造成的空隙)，可以**用FILL(EXPRESSION)命令**决定这些存储区域的内容，EXPRESSION的前两字节有效，这两字节在必要时可以重复被使用以填充这类存储区域
> 如FILE(0×9090)      
- 在输出section描述中可以有=FILEEXP属性，它的作用如同FILE()命令，但是FILE命令只作用于该FILE指令之后的section区域，而=FILEEXP属性**作用于整个输出section区域**，且**FILE命令的优先级更高**
> 这个块有些问题，还是**需要重新看看的**？ 

**4. 特殊的输出section关键字**      
- **CREATE\_OBJECT\_SYMBOLS**：为每个输入文件建立一个符号，符号名为输入文件的名字。每个符号所在的section是出现该关键字的section
- **CONSTRUCTORS**：与c++内的(全局对象的)构造函数和(全局对像的)析构函数相关

- 对于a.out目标文件格式，链接器用一些不寻常的方法实现c++的全局构造和全局析构。当链接器**生成的目标文件格式不支持任意section名字时**，比如说ECOFF、XCOFF格式，链接器将**通过名字来识别全局构造和全局析构**，对于这些文件格式，**与全局构造和全局析构的相关信息**会放入**出现CONSTRUCTORS关键字的输出section内**
> 符号__CTORS\_LIST__表示全局构造信息的的开始处，__CTORS\_END__表示全局构造信息的结束处；符号__DTORS\_LIST__表示全局析构信息的的开始处，__DTORS\_END__表示全局析构信息的结束处；这两块信息的开始处是**一字长的信息**，表示**该块信息有多少项数据**，然后**以值为零的一字长数据结束**

- 通常GNU C++在函数\__main内安排全局构造代码的运行

- 对于支持任意section名的目标文件格式，比如COFF、ELF格式，GNU C++将**全局构造**和**全局析构**信息分别放入.ctors section和.dtors section内，然后在链接脚本内加入如下：
```
// 全局构造
__CTOR_LIST__ = .;
LONG((__CTOR_END__ – __CTOR_LIST__) / 4 – 2)
*(.ctors)
LONG(0)
__CTOR_END__ = .;

// 全局析构
__DTOR_LIST__ = .;
LONG((__DTOR_END__ – __DTOR_LIST__) / 4 – 2)
*(.dtors)
LONG(0)
__DTOR_END__ = .;
```       
- 如果使用GNU C++提供的**初始化优先级支持(它能控制每个全局构造函数调用的先后顺序)**，则需要在链接脚本内把CONSTRUCTORS替换成SORT(CONSTRUCTORS)，把\*(.ctors)换成\*(SORT(.ctors))，把\*(.dtors)换成\*(SORT(.dtors))，通常默认的连接脚本已作好的以上工作

- **修改定位器：**通过**对定位器符号赋值**来修改定位器的值
```
SECTIONS
{
. = SIZEOF_HEADERS;
.text : { *(.text) }
. = 0×10000;
.data : { *(.data) }
. = 0×8000000;
.bss : { *(.bss) }
}
```

- **输出section的丢弃：**对于.foo： { *(.foo) }，如果没有任何一个输入文件包含.foo section，那么链接器将不会创建.foo输出section，但是**如果在这些输出section描述内包含了非输入section描述命令(如符号赋值语句)，那么连接器将总是创建该输出section**
- 有一个特殊的输出section，名为/DISCARD/，被该section引用的任何输入section将**不会出现在输出文件内**       

### 6.2 输出section描述(进阶)
- 前面我们介绍了SECTION、ADDRESS、OUTPUT-SECTION-COMMAND相关信息，下面将介绍其他属性
 
#### 6.2.1 输出section的类型
- 通过[(TYPE)]**设置输出section的类型**
> 如果没有指定TYPE类型，则链接器根据输出section**引用的输入section的类型设置该输出section的类型**。它可以为以下五种值：**NOLOAD**表示该section在程序运行时，不被载入内存。**DSECT,COPY,INFO,OVERLAY**这些类型很少被使用，为了向后兼容才被保留下来；这些类型的section必须被标记为**不可加载的**，以便在程序运行时**不为它们分配内存**
 
#### 6.2.2 输出section的LMA 
- 默认情况下，**LMA等于VMA**，但可以通过[AT(LMA)]项，即关键字AT()**指定LMA**
> 用关键字AT()指定，**括号内表达式的值用于设置LMA**      
> 如果不用AT()关键字，那么可用**AT>LMA_REGION表达式设置指定该section加载地址的范围**，这个属性主要**用于构件ROM境象**

```
SECTIONS
{
.text 0×1000 : {_etext = . ;*(.text);  }
.mdata 0×2000 :
AT ( ADDR (.text) + SIZEOF (.text) )
{ _data = . ; *(.data); _edata = . ; }
.bss 0×3000 :
{ _bstart = . ; *(.bss) *(COMMON) ; _bend = . ;}
}

// 程序如下
extern char _etext, _data, _edata, _bstart, _bend;
char *src = &_etext;
char *dst = &_data;
/* ROM has data at end of text; copy it. */
while (dst rom)
```

#### 6.2.3 设置输出section所在的程序段
- 通过[:PHDR HDR ...]项将输出section放入**预先定义的程序段**(program segment)内
> 若某个输出section设置了它所在的一个或多个程序段，则**接下来定义的输出section的默认程序段与该输出section的相同，除非再次显示地指定**      

```
PHDRS { text PT_LOAD ; }
SECTIONS { .text : { *(.text) } :text }
```
- 通过**NONE**指定链接器**不把该section放入任何程序段内**
 
#### 6.2.4 设置输出section的填充模版
- 任何输出section描述内的未指定的内存区域，链接器都会用该模版填充该区域，通过[=FILLEXP]项设置填充值
> **=FILEEXP**，前两字节有效，当区域大于两字节时，重复使用这两字节以将其填满       

```
SECTIONS { .text : { *(.text) } =0×9090 }
```
 
### 6.3 覆盖图(overlay)描述
- 覆盖图描述**使两个或多个不同的section占用同一块程序地址空间。**覆盖图管理代码**负责将section的拷入和拷出**
- 考虑这种情况，当某存储块的访问速度比其他存储块要快时，那么如果将section拷到该存储块来执行或访问，那么速度将会有所提高，覆盖图描述就很适合这种情形   
```
SECTIONS {
…
OVERLAY [START] : [NOCROSSREFS] [AT ( LDADDR )]
{

SECNAME1
{
OUTPUT-SECTION-COMMAND
OUTPUT-SECTION-COMMAND
…
} [:PHDR...] [=FILL]


SECNAME2
{
OUTPUT-SECTION-COMMAND
OUTPUT-SECTION-COMMAND
…
} [:PHDR...] [=FILL]
…
} [>REGION] [:PHDR...] [=FILL]
…
}
```
- 由以上可知，同一**覆盖图内的section具有相同的VMA**，这里VMA由[START] 决定
- SECNAME2的LMA为SECTNAME1的LMA加上SECNAME1的大小
- SECNAME1的LMA由**LDADDR决定**，如果它没有被指定，那么由**START决定**，如果它也没有被指定，那么由**当前定位符号的值决定**
- NOCROSSREFS关键字说明**各section之间不能交叉引用，否则报错**
- 对于OVERLAY描述的每个section，连接器将定义两个符号**\_\_load\_start\_SECNAME**和**\_\_load\_stop\_SECNAME**，这两个符号的值分别代表**SECNAME section的LMA地址的开始和结束**
- 连接器处理完OVERLAY描述语句后，将**定位符号的值加上所有覆盖图内section大小的最大值**

```
SECTIONS{
…
// .text0 section和.text1 section的VMA地址是0×1000
OVERLAY 0×1000 : AT (0×4000)
{
// .text0 section加载于地址0×4000，.text1 section紧跟在其后
.text0 { o1/*.o(.text) }
.text1 { o2/*.o(.text) }
}
…
}

// 程序代码，拷贝.text1 section代码
extern char __load_start_text1, __load_stop_text1;
memcpy ((char *) 0×1000, &__load_start_text1,&__load_stop_text1 – &__load_start_text1);
```

------
## 7. 内存区域命令
- 通常链接器可以为section在**程序地址空间内分配任意位置的存储区域**，并通过输出section描述的**> REGION属性**显示地将该输出section**限定于在程序地址空间内的某块存储区域**；当存储区域大小不能满足要求时，连接器会报告该错误。
- 可以用**MEMORY命令**让在SECTIONS命令内**未引用的selection**分配**在程序地址空间内的某个存储区域内**
> 存储区域指的是在程序地址空间内的

```
MEMORY {
// NAME ：存储区域的名字，这个名字可以与符号名、文件名、section名重复，因为它处于一个独立的名字空间
// ATTR ：定义该存储区域的属性
NAME1 [(ATTR)] : ORIGIN = ORIGIN1, LENGTH = LEN1
NAME2 [(ATTR)] : ORIGIN = ORIGIN2, LENGTH = LEN2
…
}
```
- 当某输入section没有在SECTIONS命令内引用时，链接器**把该输入section直接拷贝成输出section**，然后将该输出section放入内存区域内
- 如果内存区域设置了ATTR属性，那么该区域只接受**满足该属性的section**
- ATTR属性内：**R**只读section；**W**读/写section；**X**可执行section；**A**可分配的section；**I**初始化了的section；**L**同I；**!**不满足该字符之后的任何一个属性的section
- **ORIGIN关键字**：区域的开始地址，可简写成org或o
- **LENGTH关键字**：区域的大小，可简写成len或l

```
MEMORY
{
// 把在SECTIONS命令内未引用的且具有读属性或写属性的输入section放入rom区域内
// 把其他未引用的输入section放入 ram
rom (rx) : ORIGIN = 0, LENGTH = 256K
ram (!rx) : org = 0×40000000, l = 4M
}
```
- 如果某输出section要被放入某内存区域内，而该输出section又没有指明ADDRESS属性，那么链接器**将该输出section放在该区域内下一个能使用位置**

------
## 8. PHDRS命令
- 该命令仅在产生**ELF目标文件时有效**
- ELF目标文件格式**用program headers程序头(程序头内包含一个或多个segment程序段描述)来描述程序如何被载入内存**，可以用objdump -p命令查看    
- 当在本地ELF系统运行ELF目标文件格式的程序时，系统加载器**通过读取程序头信息**以知道如何将程序加载到内存
> 要了解系统加载器如何解析程序头，请参考ELF ABI文档

- 在链接脚本内**不指定PHDRS命令时**，链接器能够很好的创建程序头，但是有时需要更精确的描述程序头，那么PAHDRS命令就派上用场了
> 注意：一旦在链接脚本内使用了PHDRS命令，那么链接器**仅会**创建PHDRS命令指定的信息，所以使用时须谨慎

```
// FILEHDR、PHDRS、AT、FLAGS为关键字
PHDRS
{
NAME TYPE [ FILEHDR ] [ PHDRS ] [ AT ( ADDRESS ) ]
[ FLAGS ( FLAGS ) ] ;
}
```
- **NAME**：为程序段名，此名字可以与符号名、section名、文件名重复，因为它在一个独立的名字空间内。**此名字只能在SECTIONS命令内使用**      
- 一个程序段可以由多个**可加载**的section组成。通过输出section描述的属性，PHDRS可以将输出section加入一个程序段，PHDRS中的PHDRS为程序段名
- 在一个输出section描述内可以多次使用PHDRS命令，即可以将一个section加入多个程序段
- 如果在一个输出section描述内指定了PHDRS属性，那么其后的输出section描述将默认使用该属性，除非它也定义了PHDRS属性
> 即当多个输出section属于同一程序段时可简化书写


- **TYPE属性有8种格式，如下：**  
```
PT_NULL 0   // 表示未被使用的程序段
PT_LOAD 1   // 表示该程序段在程序运行时应该被加载
PT_DYNAMIC  // 表示该程序段包含动态连接信息
PT_INTERP 3 // 表示该程序段内包含程序加载器的名字，在linux下常见的程序加载器是ld-linux.so.2
PT_NOTE 4   // 表示该程序段内包含程序的说明信息
PT_SHLIB 5  // 一个保留的程序头类型，没有在ELF ABI文档内定义
PT_PHDR 6   // 表示该程序段包含程序头信息。
EXPRESSION 表达式值 // 以上每个类型都对应一个数字，该表达式定义一个用户自定的程序头。
```

- 在TYPE属性后**存在FILEHDR关键字**，表示该段包含ELF**文件头**信息；**存在PHDRS关键字**，表示该段包含ELF**程序头**信息
- **AT(ADDRESS)**属性定义该程序段的加载位置(LMA)，该属性将**覆盖**该程序段内的section的AT()属性
> 通常链接器会根据该程序段包含的section的属性**设置FLAGS标志**，该标志用于设置程序段描述的p_flags域

```
PHDRS
{
headers PT_PHDR PHDRS ;
interp PT_INTERP ;
text PT_LOAD FILEHDR PHDRS ;
data PT_LOAD ;
dynamic PT_DYNAMIC ;
}
SECTIONS
{
. = SIZEOF_HEADERS;
.interp : { *(.interp) } :text :interp
.text : { *(.text) } :text
.rodata : { *(.rodata) } /* defaults to :text */
…
. = . + 0×1000; /* move to a new page in memory */
.data : { *(.data) } :data
.dynamic : { *(.dynamic) } :data :dynamic
…
}
```
------
## 9. 版本号命令
- 当使用ELF目标文件格式时，链接器支持带版本号的符号，**版本号也只限于ELF文件格式**
> 仅仅在共享库中，符号的版本号属性才有意义。动态加载器使用符号的版本号为**应用程序选择共享库内的一个函数的特定实现版本**；可以在链接脚本内直接使用版本号命令，也可以将版本号命令实现于**一个特定版本号描述文件(用连接选项–version-script指定该文件)**
```
VERSION { version-script-commands }
```

### 9.1 带版本号的符号的定义(共享库内)
```
// 文件b.c
int getVersion()
{
    return 1;
}

// 链接器版本控制脚本b.lds
VER1.0 {
    getVersion;
};

VER2.0{

};

$gcc -c b.c
$gcc -shared -Wl,--version-script=b.lds -o libb.so b.o

// 可以在{}内填入要绑定的符号，本例中getVersion符号就与VER1.0绑定了，那么如果有一个应用程序链接到该库的getVersion符号，那么它连接的就是VER1.0版本的getVersion符号

------------------------------------------------------------

// 升级b.c
int getVersion()
{
    return 101;
}

// 对getVersion()进行了更改，其返回值的意义也进行改变，也就是它和之前不兼容
// 为了程序的安全，我们把b.lds更改为：
VER1.0{
};

VER2.0{
    getVersion;
};

// 生成新的libb.so文件，此时运行app.exe(它已经连接到VER1.0版本的getVersion())，就会发现该应用程序不能运行了

// 提示信息如下：
./app.exe: relocation error: ./app.exe: symbol getVersion, version VER1.0 not defined in file libb.so with link time reference
// 即因为库内没有VER1.0版本的getVersion()，只有VER2.0版本的getVersion()

```

### 9.2 查看连接的符号的版本
```
// 用nm命令发现app连接到VER1.0版本的getVersion
nm app.exe | grep getVersion
U new_true@@VER1.0
```

### 9.3 GNU的扩充
- 在GNU中，允许在程序文件内绑定**符号**到**带版本号的别名符号**
```
// 文件b.c
int old_getVersion()
{
    return 1;
}

int new_getVersion()
{
    return 101;
}

// 对于VER1.0版本号的getVersion别名符号是old_getVersion
__asm__(".symver old_getVersion,getVersion@VER1.0");

// 对于VER2.0版本号的getVersion别名符号是new_getVersion
__asm__(".symver new_getVersion,getVersion@@VER2.0");

// 在链接时，默认的版本号为VER2.0
// 供链接器用的版本控制脚本b.lds
VER1.0{
};

VER2.0{
};

// 版本控制文件内必须包含版本VER1.0和版本VER2.0的定义，因为在b.c文件内有对他们的引用
// 再次执行以下命令编译连接b.c和app.c
# gcc -c src/b.c
# gcc -shared -Wl,--version-script=./lds/b.lds -o libb.so b.o
# gcc -o app.exe ./src/app.c libb.so
# ./app.exe
Version=0x65

// app.exe的确是连接的VER2.0的getVersion，即new_getVersion()
 
--------------------------------------------------------------
// 对app.c进行修改，以使它链接VER1.0的getVersion，即old_getVersion()
// app.c文件
#include <stdio.h>
__asm__(".symver getVersion,getVersion@VER1.0");
extern int getVersion();
int main()
{
    printf("Version=%p\n", getVersion());
    return 0;
}

// 再次编译链接b.c和app.c
# ./app.exe
Version=0x1
// 说明此次app.exe的确是连接的VER1.0的getVersion，即old_getVersion()
```

------
## 10. 表达式
- 语法与C相同，表达式的值都是整型，如果ld的**运行主机和生成文件的目标机**都是32位，则表达式是32位数据，否则是64位数据
```
// 常用的表达式：1K=1024 1M=1024*1024
_fourk_1 = 4K;      /* K、M单位 */
_fourk_2 = 4096;    /* 整数 */
_fourk_3 = 0×1000;  /* 16 进位 */
_fourk_4 = 01000;   /* 8 进位 */
```

### 10.1 符号名
- 没有被引号包围的符号，以字母、下划线或点(.)开头，可包含字母、下划线、点(.)和(-)
> 当符号名被**引号**包围时，符号名可以与关键字相同，如“SECTION”=9;
 
### 10.2定位符号.
- 只在SECTIONS命令内有效，代表**一个程序地址空间内的地址**
> 在链接时，当定位符用在SECTIONS命令的输出section描述内时，它代表的是该section的当前**偏移**，而不是程序地址空间的绝对地址      
> 当程序载入后，符号最后的地址还是程序地址空间的绝对地址      

```
// 示例1
SECTIONS
{
output :
{
file1(.text)
. = . + 1000;
file2(.text)
. += 1000;
file3(.text)
} = 0×1234; // 对定位符的赋值而产生的空隙由0×1234填充
}
```

```
// 示例2
SECTIONS
{
. = 0×100
.text: {
*(.text)
. = 0×200
}
. = 0×500
.data: {
*(.data)
. += 0×600
}
} 
// .text section在程序地址空间的开始位置是0x100
```

```
// 示例3
// 文件src\a.c
#include <stdio.h>
int a = 100;
int b=0;
int c=0;
int d=1;

int main()
{
    printf( "&a=%p\n", &a );
    printf( "&b=%p\n", &b );
    printf( "&c=%p\n", &c );
    printf( "&d=%p\n", &d );

    return 0;
}


// 文件lds\a.lds
a = 10; /* 全局位置 */
SECTIONS
{
b = 11;
.text :
{
*(.text)
c = .; /* section描述内 */
. = 10000;
d = .;
}
_bdata = (. + 3) & ~ 4; /* SECTIONS命令内 */
.data : { *(.data) }
}


// 不使用a.lds情况下编译
# gcc -Wall -o a-without-lds.exe ./src/a.c
# ./a-without-lds.exe
&a=0x601020
&b=0x601038
&c=0x60103c
&d=0x601024


// 使用a.lds情况下编译
# gcc -Wall -o a-with-lds.exe ./src/a.c ./lds/a.lds
# ./a-with-lds.exe
&a=0xa
&b=0xb
&c=0x400638
&d=0x402b20
```

### 10.3 表达式的操作符
- 表达式的操作符与C一致

### 10.4 表达式的计算
- 链接器**延迟计算**大部分表达式的值，但是对待**与链接过程紧密相关**的表达式，连接器会**立即计算表达式**，如果不能计算则报错
> 比如，对于section的**VMA地址**、**内存区域块的开始地址和大小**，与其相关的表达式应该立即被计算
```
// (9+this_isnt_constant)表达式的值用于设置.text section的VMA地址，因此需要立即运算
// 但是由于this_isnt_constant变量的值不确定，所以此时链接器无法确立表达式的值，因此会报错
SECTIONS
{
.text 9+this_isnt_constant :
{ *(.text) }
}
```

### 10.5 相对值与绝对值
- 在输出section描述内的表达式，链接器取其相对值，即**相对与该section的开始位置的偏移**；在SECTIONS命令内且**非输出section描述内的表达式**，链接器**取其绝对值**
> 通过**ABSOLUTE关键字**可以将相对值转化成绝对值，即在原来值的基础上加上**表达式所在section的VMA值**
```
SECTIONS
{
// _edata符号的值是.data section的末尾位置(绝对值，在程序地址空间内)
.data : { *(.data) ;_edata = ABSOLUTE(.); }
}
```

### 10.6 内建函数
- **ABSOLUTE(EXP)**：转换成绝对值
- **ADDR(SECTION)**：返回某section的VMA值
- **ALIGN(EXP)**：返回定位符**.**的按照EXP进行对齐后的修调值
> 对齐后的修调值算法为：(. + EXP – 1) & ~(EXP – 1)

- **BLOCK(EXP)**：如同ALIGN(EXP)，为了向前兼容
- **DEFINED(SYMBOL)**：如果符号SYMBOL在全局符号表内，且被定义了，那么返回1，否则返回0
```
SECTIONS { …
.text : {
begin = DEFINED(begin) ? begin : . ;
…
}
…
}
```

- **LOADADDR(SECTION)**：返回SECTION的LMA
- **MAX(EXP1,EXP2)**：返回大者
- **MIN(EXP1,EXP2)**：返回小者
- **NEXT(EXP)**：返回下一个能被使用的地址，该地址是EXP的倍数，类似于ALIGN(EXP)
> 除非使用了MEMORY命令定义了一些非连续的内存块，否则NEXT(EXP)与ALIGH(EXP)一定相同

- **SIZEOF(SECTION)**：返回SECTION的大小
> 当SECTION没有被分配时，即此时SECTION的大小还不能确定时，连接器会报错

- **SIZEOF\_HEADERS**：返回输出文件头部的字节数，这些信息出现在输出文件的开始处
> 当设置第一个段的开始地址时，你可以使用这个数字。如果你选择了加速分页，当产生一个ELF输出文件时，如果链接器脚本使用**SIZEOF\_HEADERS内建函数**，链接器必须在它算出所有段地址和长度之前**计算程序头部的数值。**如果链接器后来发现它需要附加程序头，它将报告一个**not enough room for program headers**错误。为了避免这样的错误，你必须**避免使用SIZEOF_HEADERS函数**，或者你必须修改你的链接器脚本去避免强制链接器去使用附加程序头，或者你必须**使用PHDRS命令去定义你自己的程序头**

------
## 11. 暗含的连接脚本
- 输入文件可以是目标文件，也可以是链接脚本，此时的链接脚本被称为**暗含的连接脚本**
> 如果链接器不认识某个输入文件，那么该文件**被当作链接脚本**解析；如果发现它的格式**又不是连接脚本的格式，链接器报错**

- 一个暗含的链接脚本**不会替换默认的链接脚本**，仅仅是增加新的链接而已
- 一般来说，暗含的链接脚本符号分配命令，或INPUT、GROUP、VERSION命令
- 在链接命令行中，**每个输入文件的顺序都被固定好了**，暗含的链接脚本在链接命令行内占住一个位置，这个位置**决定了由该链接脚本指定的输入文件在链接过程中的顺序**
> 典型的暗含的连接脚本是libc.so文件，在GNU/linux内一般存在/usr/lib目录下